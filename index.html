<!DOCTYPE html>
<html>
<head>
<title>FotoFrez - web camera fun effects</title>

<link rel="manifest" href="manifest.json">
<meta name="viewport" content="width=device-width,initial-scale=1">
<link rel="apple-touch-icon" href="images/FotoFrez-128.png">
<meta name="apple-mobile-web-app-capable" content="yes">

<link rel="shortcut icon" href="images/favicon.ico" type="image/x-icon">
<link rel="icon" href="images/favicon.ico" type="image/x-icon">

 <link href="css/index.css" rel="stylesheet"/>
<script type="text/javascript" src="glMatrix-0.9.5.min.js"></script>
<script type="text/javascript" src="webgl-utils.js"></script>
<script type="text/javascript" src="main.js"></script>

<script id="shader-1-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;
    const mediump float Tau	= 6.2832;
    void main(void) {
    vec2 cent = vec2(0.5, 0.5);
    vec2 tx = 1.0-gl_FragCoord.xy / resolution;
    if (index == 0){
      if (tx.x < 0.5)
      		tx.x = 1.0-tx.x;
    }
    else if (index == 1) {
    	if (tx.y > cent.y)
    		tx.y = 1.0-tx.y;
   	}
   	else if (index == 2) {
		if (tx.y  > 0.5)
			tx = vec2(1.0-tx.x, 1.0-tx.y);
   	}
   	else if (index == 3)
    { // 4 way
    if (tx.x < 0.5)
    	tx.x = tx.x * 2.0;
    else
    	tx.x = 1.0 - (tx.x - 0.5) * 2.0;
    if (tx.y < 0.5)
    	tx.y = tx.y * 2.0;
    else
    	tx.y = 1.0 - (tx.y - 0.5) * 2.0;
	    	//1.0 - abs(vertexcoord.yx);
    }
    else if (index == 4)
    { // kaleidoscope - 4 way
    	//mediump sides = 8;
		mediump vec2 p = tx - cent;
		mediump float r = length(p);
		mediump float a = atan(p.y, p.x) + 1.; // + angle;
		a = mod(a, Tau/4.);
		a = abs(a - Tau/4./2.);
		tx = r * vec2(cos(a), sin(a));
	}
   	else if (index == 5)
    { // kaleidoscope - 8 way
    	//mediump sides = 8;
		mediump vec2 p = tx - cent;
		mediump float r = length(p);
		mediump float a = atan(p.y, p.x) + 1.; // + angle;
		a = mod(a, Tau/8.);
		a = abs(a - Tau/8./2.);
		tx = r * vec2(cos(a), sin(a));
	}

   	mediump vec4 rgb = texture2D(texture0, tx);

	gl_FragColor =  vec4(rgb.xyz, 1.0);

    }
</script>

<script id="shader-7-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;
    const mediump vec2 sampleDivisor = vec2(0.02, 0.02);
    void main(void) {
    mediump vec2 cent = vec2(0.5, 0.5);

	mediump vec2 tx = 1.0-gl_FragCoord.xy / resolution;
	int makeBlack = 0;

	lowp float checkForPresenceWithinDot = 1.0;
	if (index == 0)
    { // holes in the screen pixelated - 1
	     mediump float fractionalWidthOfPixel = 0.02;
	 	 mediump float aspectRatio = 0.8;
	     mediump float dotScaling = 0.9;
	     mediump vec2 sampleDivisor = vec2(fractionalWidthOfPixel, fractionalWidthOfPixel / aspectRatio);

	     mediump vec2 samplePos = tx - mod(tx, sampleDivisor) + 0.5 * sampleDivisor;
	     mediump vec2 textureCoordinateToUse = vec2(tx.x, (tx.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
	     mediump vec2 adjustedSamplePos = vec2(samplePos.x, (samplePos.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
	     mediump float distanceFromSamplePoint = distance(adjustedSamplePos, textureCoordinateToUse);
	     //checkForPresenceWithinDot = step(distanceFromSamplePoint, (fractionalWidthOfPixel * 0.5) * dotScaling);
	     if (step(distanceFromSamplePoint, (fractionalWidthOfPixel * 0.5) * dotScaling) >= 1.0)
		     {
		     	tx = samplePos;
		     }
		     else
		     {
		     	makeBlack = 1;
		     }

	}

	else if (index == 1)
    { // holes in the screen pixelated - 2
	     mediump float fractionalWidthOfPixel = 0.02;
	 	 mediump float aspectRatio = 0.8;
	     mediump float dotScaling = 0.9;
	     mediump vec2 sampleDivisor = vec2(fractionalWidthOfPixel, fractionalWidthOfPixel / aspectRatio);

	     mediump vec2 samplePos = tx - mod(tx, sampleDivisor) + 0.5 * sampleDivisor;
	     mediump vec2 textureCoordinateToUse = vec2(tx.x, (tx.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
	     mediump vec2 adjustedSamplePos = vec2(samplePos.x, (samplePos.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
	     mediump float distanceFromSamplePoint = distance(adjustedSamplePos, textureCoordinateToUse);
	     //checkForPresenceWithinDot = step(distanceFromSamplePoint, (fractionalWidthOfPixel * 0.5) * dotScaling);
	     if (step(distanceFromSamplePoint, (fractionalWidthOfPixel * 0.5) * dotScaling) >= 1.0)
	     { // don't black it out
	     	// checkForPresenceWithinDot = 1.0;
	     }
	     else
	     	tx = samplePos;
    }
    else if (index == 2)
    { // stripes in the screen
	     mediump float fractionalWidthOfPixel = 0.1;
	     mediump float dotScaling = 0.6;
	     mediump float sampleDivisor = fractionalWidthOfPixel;

	     mediump float samplePos = tx.x - mod(tx.x, sampleDivisor) + 0.5 * sampleDivisor;
	     mediump float distanceFromSamplePoint = distance(samplePos, tx.x);
	     checkForPresenceWithinDot = step(distanceFromSamplePoint, (fractionalWidthOfPixel * 0.5) * dotScaling);
    }
    else if (index == 3)
    { // stripes in the screen 2
	     mediump float fractionalWidthOfPixel = 0.2;
	     mediump float dotScaling = 0.5;
	     mediump float sampleDivisor = fractionalWidthOfPixel;

	     mediump float samplePos = tx.x - mod(tx.x, sampleDivisor) + 0.5 * sampleDivisor;
	     mediump float distanceFromSamplePoint = distance(samplePos, tx.x);
	     checkForPresenceWithinDot = step(distanceFromSamplePoint, (fractionalWidthOfPixel * 0.5) * dotScaling);
    }
    else if (index == 4)
    { // holes in the screen
	     mediump float fractionalWidthOfPixel = 0.05;
	 	 mediump float aspectRatio = 0.8;
	     mediump float dotScaling = 0.9;
	     mediump vec2 sampleDivisor = vec2(fractionalWidthOfPixel, fractionalWidthOfPixel / aspectRatio);

	     mediump vec2 samplePos = tx - mod(tx, sampleDivisor) + 0.5 * sampleDivisor;
	     mediump vec2 textureCoordinateToUse = vec2(tx.x, (tx.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
	     mediump vec2 adjustedSamplePos = vec2(samplePos.x, (samplePos.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
	     mediump float distanceFromSamplePoint = distance(adjustedSamplePos, textureCoordinateToUse);
	     checkForPresenceWithinDot = step(distanceFromSamplePoint, (fractionalWidthOfPixel * 0.5) * dotScaling);
    }
    else if (index == 5) // pixelate
        tx = tx - mod(tx, sampleDivisor/2.0);
    else if (index == 6) // pixelate
        tx = tx - mod(tx, sampleDivisor);
    else if (index == 7)
    { // polar pixelation 2
		 mediump vec2 normCoord = 2.0 * tx - 1.0;
		 mediump vec2 normCenter = 2.0 * cent - 1.0;
		 mediump vec2 pixelSize = vec2(0.03, 0.03);

		 normCoord -= normCenter;

		 mediump float r = length(normCoord); // to polar coords
		 mediump float phi = atan(normCoord.y, normCoord.x); // to polar coords

		 r = r - mod(r, pixelSize.x) + 0.03;
		 phi = phi - mod(phi, pixelSize.y);

		 normCoord.x = r * cos(phi);
		 normCoord.y = r * sin(phi);

		 normCoord += normCenter;

		 tx = normCoord / 2.0 + 0.5;
    }
    else if (index == 8)
    { // polar pixelation
	     mediump vec2 normCoord = 2.0 * tx - 1.0;
	     mediump vec2 normCenter = 2.0 * cent - 1.0;
	     mediump vec2 pixelSize = vec2(0.05, 0.05);

	     normCoord -= normCenter;

	     mediump float r = length(normCoord); // to polar coords
	     mediump float phi = atan(normCoord.y, normCoord.x); // to polar coords

	     r = r - mod(r, pixelSize.x) + 0.03;
	     phi = phi - mod(phi, pixelSize.y);

	     normCoord.x = r * cos(phi);
	     normCoord.y = r * sin(phi);

	     normCoord += normCenter;

	     tx = normCoord / 2.0 + 0.5;
    }

	mediump vec4 rgb = texture2D(texture0, tx);
    if (checkForPresenceWithinDot < 1.0) // do screen full of holes
     	rgb = rgb * 0.0;

	gl_FragColor =  vec4(rgb.xyz, 1.0);

    }
</script>

<script id="shader-3-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;

    void main(void) {
    mediump vec2 cent = vec2(0.5, 0.5);
	mediump vec2 tx = 1.0-gl_FragCoord.xy / resolution;
	if (index == 0)
    { // box
     mediump vec2 normCoord = 2.0 * tx - 1.0;
     mediump vec2 normCenter = 2.0 * vec2(0.5,0.5) - 1.0; // could use variable here for (0.5, 0.5)

     normCoord -= normCenter;
     mediump vec2 s = sign(normCoord);
     normCoord = abs(normCoord);
     normCoord = 0.5 * normCoord + 0.5 * smoothstep(0.25, 0.5, normCoord) * normCoord;
     normCoord = s * normCoord;

     normCoord += normCenter;

     tx = normCoord / 2.0 + 0.5;
    }
    else if (index == 1) // bulge
    {
      mediump float aspectRatio = 0.3;
     	mediump float radius = 0.5;
     	mediump float scale = 0.5;
      mediump vec2 textureCoordinateToUse = vec2(tx.x * aspectRatio + 0.5 - 0.5 * aspectRatio, tx.y);
    	mediump float dist = distance(cent, textureCoordinateToUse);
    	textureCoordinateToUse = tx;

    	if (dist < radius)
    	{
        	textureCoordinateToUse -= cent;
        	mediump float percent = 1.0 - ((radius - dist) / radius) * scale;
        	percent = percent * percent;

        	textureCoordinateToUse = textureCoordinateToUse * percent;
        	textureCoordinateToUse += cent;
        	tx = textureCoordinateToUse;
    	}

    }

	else if (index == 2) // pinch
    {
     mediump float aspectRatio = 1.0;
     mediump float radius = 0.5;
     mediump vec2 textureCoordinateToUse = vec2(tx.x, (tx.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
     mediump float dist = distance(vec2(0.5,0.5), textureCoordinateToUse);
     textureCoordinateToUse = tx;

     if (dist < radius)
     {
         textureCoordinateToUse -= vec2(0.5,0.5);
         mediump float percent = 1.0 + ((0.5 - dist) / 0.5) * 1.0;
         textureCoordinateToUse = textureCoordinateToUse * percent;
         textureCoordinateToUse += vec2(0.5,0.5);
         tx = textureCoordinateToUse;
     }
    }
    else if (index == 3)
    { // vertical stretch distortion filter
     mediump vec2 normCoord = 2.0 * tx - 1.0;
     mediump vec2 normCenter = 2.0 * vec2(0.5,0.5) - 1.0; // could use variable here for (0.5, 0.5)

     normCoord -= normCenter;
     mediump vec2 s = sign(normCoord);
     normCoord = abs(normCoord);
     normCoord = 0.5 * normCoord + 0.5 * smoothstep(0.25, 0.5, normCoord) * normCoord;
     normCoord = s * normCoord;

     normCoord += normCenter;

     tx.y = normCoord.y / 2.0 + 0.5;
    }

    else if (index == 4)
    { // vertical stretch distortion filter
     mediump vec2 normCoord = 2.0 * tx - 1.0;
     mediump vec2 normCenter = 2.0 * vec2(0.5,0.3) - 1.0; // could use variable here for (0.5, 0.5)

     normCoord -= normCenter;
     mediump vec2 s = sign(normCoord);
     normCoord = abs(normCoord);
     normCoord = 0.5 * normCoord + 0.5 * smoothstep(0.25, 0.5, normCoord) * normCoord;
     normCoord = s * normCoord;

     normCoord += normCenter;

     tx.y = normCoord.y / 2.0 + 0.5;
    }

    else if (index == 5) // sphere distortion
    {
     mediump float radius = 0.5;
     mediump float refractiveIndex = 0.5;
     mediump float aspectRatio = 0.8;
     tx = vec2(tx.x, (tx.y * aspectRatio + 0.5 - 0.5 * aspectRatio));
     mediump float distanceFromCenter = distance(vec2(0.5, 0.5), tx);
     lowp float checkForPresenceWithinSphere = step(distanceFromCenter, radius);

     distanceFromCenter = distanceFromCenter / radius;

     mediump float normalizedDepth = radius * sqrt(1.0 - distanceFromCenter * distanceFromCenter);
     mediump vec3 sphereNormal = normalize(vec3(tx - vec2(0.5, 0.5), normalizedDepth));

     mediump vec3 refractedVector = refract(vec3(0.0, 0.0, -1.0), sphereNormal, refractiveIndex);
     if (checkForPresenceWithinSphere > 0.0)
     	tx = 1.0 - (refractedVector.xy + 1.0) * 0.5;
    }
	mediump vec4 rgb = texture2D(texture0, tx);

	gl_FragColor =  vec4(rgb.xyz, 1.0);
    }
</script>

<script id="shader-4-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;

    void main(void) {
    lowp vec3 colors[4];
    colors[0] = vec3(0.0, 0.0, 1.0);
    colors[1] = vec3(1.0, 1.0, 0.0);
    colors[2] = vec3(1.0, 0.0, 0.0);
    colors[3] = vec3(1.0, 0.0, 1.0);
	 mediump vec2 tx = 1.0-gl_FragCoord.xy / resolution;
	 vec4 rgb = texture2D(texture0, tx);
	  if (index == 0)
  	  rgb.xyz = vec3((rgb.x+rgb.y+rgb.z)/3.0);
	  else if (index == 1)
	    rgb.xyz = vec3(1.0) - vec3((rgb.x+rgb.y+rgb.z)/3.0); //invert intensity
	  else if (index == 2) {
	   	mediump float lum = (rgb.r + rgb.g + rgb.b) / 3.0;
	    if (lum < 0.5)
  	    rgb.xyz = mix(colors[0], colors[2], lum / 0.5);
  	  else
  	   	rgb.xyz = mix(colors[1], colors[3], (lum - 0.5) / 0.5);
    }
    else if (index == 3) {
	   	mediump float lum = (rgb.r + rgb.g + rgb.b) / 3.0;
	    if (lum < 0.5)
        rgb.xyz = mix(colors[0], colors[1], lum / 0.5);
  	  else
  	   	rgb.xyz = mix(colors[1], colors[2], (lum - 0.5) / 0.5);
    }
    else if (index == 4) {
	   	mediump float lum = (rgb.r + rgb.g + rgb.b) / 3.0;
	    if (lum < 0.5)
  	    rgb.xyz = mix(colors[1], colors[0], lum / 0.5);
  	  else
  	   	rgb.xyz = mix(colors[2], colors[1], (lum - 0.5) / 0.5);
    }
    else if (index == 5) {
      rgb.xyz = floor((rgb.xyz * 3.0) + vec3(0.5)) / 3.0;
      }
    else if (index == 6) {
     rgb.xyz = floor((rgb.xyz * 3.0) + vec3(0.5)) / 3.0;
     rgb.xyz = rgb.xzy;
    }
    else if (index == 7) {
     rgb.xyz = floor((rgb.xyz * 4.0) + vec3(0.5)) / 3.0;
     rgb.xyz = rgb.yxz;
    }
    else if (index == 8) {// false colour effects via linear blend
     const mediump vec3 luminanceWeighting = vec3(0.2125, 0.7154, 0.0721);
     mediump float luminance = dot(rgb.xyz, luminanceWeighting);
     rgb.xyz = mix(colors[0], colors[2], luminance);
    }
    else if (index == 9) { // false colour effects via linear blend
     const mediump vec3 luminanceWeighting = vec3(0.2125, 0.7154, 0.0721);
     mediump float luminance = dot(rgb.xyz, luminanceWeighting);
     rgb.xyz = mix(colors[1], colors[0], luminance);
    }

	gl_FragColor = vec4(rgb.xyz, 1.0);

    }
</script>

<script id="shader-5-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;

mediump vec2 sampleDivisor = vec2(0.015, 0.015);
float H = 0.032;
float S = ((3./2.) * H/sqrt(3.));

 vec2 hexCoord(vec2 hexIndex) {
	float i = hexIndex.x;
	float j = hexIndex.y;
	vec2 r;
	r.x = i * S;
	r.y = j * H + (mod(i,2.0)) * H/2.;
	return r;
}

vec2 hexIndex(vec2 coord) {
	vec2 r;
	float x = coord.x;
	float y = coord.y;
	float it = float(floor(x/S));
	float yts = y - (mod(it,2.0)) * H/2.;
	float jt = float(floor((1./H) * yts));
	float xt = x - it * S;
	float yt = yts - jt * H;
	float deltaj = (yt > H/2.)? 1.0:0.0;
	float fcond = S * (2./3.) * abs(0.5 - yt/H);

	if (xt > fcond) {
		r.x = it;
		r.y = jt;
	}
	else {
		r.x = it - 1.0;
		r.y = jt - (mod(r.x,2.0)) + deltaj;
	}

	return r;
}

#define PIXELSIZE 3.0
const float size = 7.0;
const float radius = size * 0.5 * 0.75;

    void main(void) {

    vec2 tx = 1.0-gl_FragCoord.xy / resolution;
	if (index < 2)
    { // dithering
		vec2 lResolution = vec2(256.0, 192.0);
		if (index == 1)
			lResolution /= 2.05;
		vec2 lPixel = floor(tx * lResolution);
		vec4 lTextureColour = texture2D(texture0, lPixel / lResolution);

		int lDitherIndex = int(mod(lPixel.x, 4.0)) * 4 + int(mod(lPixel.y, 4.0));

		int lI = 0;
		float lDitherValue = 0.0;
	  if (lI++ == lDitherIndex) lDitherValue =  1.0;
		else if (lI++ == lDitherIndex) lDitherValue =  9.0;
		else if (lI++ == lDitherIndex) lDitherValue =  3.0;
		else if (lI++ == lDitherIndex) lDitherValue = 11.0;
		else if (lI++ == lDitherIndex) lDitherValue = 13.0;
		else if (lI++ == lDitherIndex) lDitherValue =  5.0;
		else if (lI++ == lDitherIndex) lDitherValue = 15.0;
		else if (lI++ == lDitherIndex) lDitherValue =  7.0;
		else if (lI++ == lDitherIndex) lDitherValue =  4.0;
		else if (lI++ == lDitherIndex) lDitherValue = 12.0;
		else if (lI++ == lDitherIndex) lDitherValue =  2.0;
		else if (lI++ == lDitherIndex) lDitherValue = 10.0;
		else if (lI++ == lDitherIndex) lDitherValue = 16.0;
		else if (lI++ == lDitherIndex) lDitherValue =  8.0;
		else if (lI++ == lDitherIndex) lDitherValue = 14.0;
		else if (lI++ == lDitherIndex) lDitherValue =  6.0;
		lDitherValue /= 16.0;

		gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);

		if (lDitherValue < lTextureColour.r) gl_FragColor += vec4(1.0, 0.0, 0.0, 1.0);
		if (lDitherValue < lTextureColour.g) gl_FragColor += vec4(0.0, 1.0, 0.0, 1.0);
		if (lDitherValue < lTextureColour.b) gl_FragColor += vec4(0.0, 0.0, 1.0, 1.0);
		return;
	}
	else if (index < 4) {
	    float fW = 0.015;
	    if (index == 3)
	    	fW = 0.03;
	    vec2 sampleDivisor = vec2(fW, fW / 0.8);

     	vec2 samplePos = tx - mod(tx, sampleDivisor) + 0.5 * sampleDivisor;
		if (step(distance(vec2(samplePos.x, (samplePos.y * 0.8 + 0.5 - 0.5 * 0.8)), vec2(tx.x, (tx.y * 0.8 + 0.1))), (fW * 0.5) * 0.9) < 1.0)
			gl_FragColor = vec4(0.25);
		else
			gl_FragColor = texture2D(texture0,samplePos);
		return;
	}
    else if (index < 6)
    { // hexagonal pixelation
        if (index == 4)
        {
     		H = 0.016;
			S = ((3./2.) * H/sqrt(3.));
		}
	    tx = hexIndex(tx);
		tx = hexCoord(tx);
    }
    else if (index >= 6 ) // RGB pixelation
	{
		if (index == 7)
			sampleDivisor *= 1.5;
		else if (index == 0)
			sampleDivisor *= 2.0;
        mediump vec2 p = tx - mod(tx, sampleDivisor);
		vec4 sum = texture2D(texture0, p);
		float offsetx = tx.y - p.y;
		gl_FragColor = vec4(0.,0.,0.,1.);
		if (offsetx < sampleDivisor.y*.3) gl_FragColor.r = sum.r;
		else if (offsetx < sampleDivisor.y*.6) gl_FragColor.g = sum.g;
		else if (offsetx < sampleDivisor.y*.9) gl_FragColor.b = sum.b;
		gl_FragColor.rgb *= 1.3;
		return;
 }


	  vec4 rgb = texture2D(texture0, tx);
  	gl_FragColor = vec4(rgb.xyz, 1.0);

    }
</script>

<script id="shader-6-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;

float PI = 3.1415926535897932384626433832795;
float PI180 = float(PI / 180.0);
float sind(float a)
{
	return sin(a * PI180);
}

float cosd(float a)
{
	return cos(a * PI180);
}

float added(vec2 sh, float sa, float ca, vec2 c, float d)
{
	return 0.5 + 0.25 * cos((sh.x * sa + sh.y * ca + c.x) * d) + 0.25 * cos((sh.x * ca - sh.y * sa + c.y) * d);
}

float remap(float value, float inputMin, float inputMax, float outputMin, float outputMax)
{
    return (value - inputMin) * ((outputMax - outputMin) / (inputMax - inputMin)) + outputMin;
}

struct Circle
{
	vec2 center;
	float radius;
};

vec4 circle_mask_color(Circle circle, vec2 position)
{
	float d = distance(circle.center, position);
	if(d > circle.radius)
	{
		return vec4(0.0, 0.0, 0.0, 1.0);
	}

	float distanceFromCircle = circle.radius - d;
	float intencity = smoothstep(0.0, 1.0,clamp(remap(distanceFromCircle, 0.0, 0.1, 0.0, 1.0),0.0,1.0));
	return vec4(intencity, intencity, intencity, 1.0);
}

vec4 mask_blend(vec4 a, vec4 b)
{
	vec4 one = vec4(1.0, 1.0, 1.0, 1.0);
	return one - (one - a) * (one - b);
}

float hash(in float n) { return fract(sin(n)*43758.5453123); }

    void main(void) {
  mediump vec2 cent = vec2(0.5,0.5);
  vec2 tx = 1.0-gl_FragCoord.xy / resolution;
  if (index <= 1) // // night vision device
    {
		float wide = resolution.y/resolution.x;
		float high = 1.0;

		vec2 position = vec2(tx.x * wide, tx.y);
		vec4 mask = vec4(1.0);
		if (index == 1)
		{
			Circle circle_a = Circle(vec2(0.5, 0.5), 0.4);
			Circle circle_b = Circle(vec2(wide - 0.5, 0.5), 0.4);
			vec4 mask_a = circle_mask_color(circle_a, position);
			vec4 mask_b = circle_mask_color(circle_b, position);
			mask = mask_blend(mask_a, mask_b);
//        if (distance(cent, tx) > 0.2)
//          mask = vec4(0.0);
		}

		vec2 u = tx * 2. - 1.;
		vec2 n = u * vec2(wide, 1.0);

		vec3 c = texture2D(texture0, tx).xyz;

		// flicker, grain, vignette, fade in
		c += hash((hash(n.x) + n.y) * time) * 0.4;
		c *= 1.9 * smoothstep(length(n * 0.18), 0.8, 0.4);
		c *= smoothstep(0.001, 3.5, time);
		c = dot(c, vec3(0.2126, 0.7152, 0.0722)) * vec3(0.2, 1.5 - hash(time) * 0.1,0.4);

		gl_FragColor = vec4(c,1.0)*mask;
		return;
    }
    else // halftone
    {
    	float threshold = 0.5;
		vec2 rotationCenter = vec2(0.5, 0.5);
		vec2 shift = tx - rotationCenter;

		float dotSize = 4.0;
		float angle = 45.0;
		if (index == 3 || index == 5)
		{
			dotSize = 8.0;
			angle = 0.0;
		}
		float rasterPattern = added(shift, sind(angle), cosd(angle), rotationCenter, PI / dotSize * 680.0);
		vec4 srcPixel = texture2D(texture0, tx);

		float avg = 0.2125 * srcPixel.r + 0.7154 * srcPixel.g + 0.0721 * srcPixel.b;
	  float gray = (rasterPattern * threshold + avg - threshold) / (1.0 - threshold);

		if (index >= 4)
			gl_FragColor = vec4(gray, gray, gray, 1.0);
 		else
 			gl_FragColor = vec4(0.0, gray, 0.0, 1.0);
		return;
    }
    }
</script>

<script id="shader-2-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;

float bw(vec2 coords) {
	vec4 lm = texture2D(texture0, coords/4.28) * vec4(0.21, 0.71, 0.07, 1);

	return lm.r+lm.g+lm.b;
}

    void main(void) {
        vec3 colors[4];
    colors[0] = vec3(0.0, 0.0, 1.0);
    colors[1] = vec3(1.0, 1.0, 0.0);
    colors[2] = vec3(1.0, 0.0, 0.0);
    colors[3] = vec3(1.0, 0.0, 1.0);

    mediump vec2 cent = vec2(0.5,0.5);
    vec2 tx = 1.0-gl_FragCoord.xy / resolution;
	  vec4 rgb = texture2D(texture0, tx);

   if (index <= 2) { // golden
    	vec2 step = 1.0 / resolution;

  		vec3 texA = texture2D( texture0, tx + vec2(-step.x, -step.y) * 1.5 ).rgb;
  		vec3 texB = texture2D( texture0, tx + vec2( step.x, -step.y) * 1.5 ).rgb;
  		vec3 texC = texture2D( texture0, tx + vec2(-step.x,  step.y) * 1.5 ).rgb;
  		vec3 texD = texture2D( texture0, tx + vec2( step.x,  step.y) * 1.5 ).rgb;

  		float shadeA = dot(texA, vec3(0.333333));
  		float shadeB = dot(texB, vec3(0.333333));
  		float shadeC = dot(texC, vec3(0.333333));
  		float shadeD = dot(texD, vec3(0.333333));

  		float shade = 15.0 * pow(max(abs(shadeA - shadeD), abs(shadeB - shadeC)), 0.5);

  		vec3 col = mix(vec3(0.1, 0.18, 0.3), vec3(0.4, 0.3, 0.2), shade);

  		if (index >= 1)
  			col = 1.0-floor((col * 3.0) + vec3(0.5)) / 3.0;
  		if (index >= 2)
  		{
  		    const mediump vec3 luminanceWeighting = vec3(0.2125, 0.7154, 0.0721);
       		mediump float luminance = dot(rgb.xyz, luminanceWeighting);
       		if (index == 2)
       			col = (col + mix(colors[2], colors[1], luminance))/2.;
       		else
       			col = (col +mix(colors[2], colors[0], luminance))/2.;
  		}
	    gl_FragColor = vec4(col,1.0);
	    return;
    }
    else if (index <= 5) { // emboss filter
  		float spinT = time*0.2;
  		vec2 cutDir = vec2(cos(spinT), sin(spinT));
  		if( dot(cutDir, tx-vec2(cent.x,2.0*cent.y)) < 0.0 && index < 4)
  		{
  			gl_FragColor = rgb;
  			return;
  		}

  	    vec2 onePixel = vec2(1.0 / resolution.x, 1.0 / resolution.y);

  	    vec2 texCoord = tx;
  	    texCoord.x +=  (onePixel.x * 6.0);
  	    texCoord.y +=  (onePixel.y * 6.0);


  	    vec4 color;
  	    color.rgb = vec3(0.5);
  	    color -= texture2D(texture0, (texCoord - onePixel)) * 5.0;
  	    color += texture2D(texture0, (texCoord + onePixel)) * 5.0;

  	    color.rgb = vec3((color.r + color.g + color.b) / 3.0);
  	    if (index == 4)
  	    	color *= rgb;
  	    if (index == 5)
  		    color = (2.0*color + (1.0-rgb))/3.;
  	    gl_FragColor = vec4(color.rgb, 1.0);
  	    return;
    }
    else if (index >= 6) { // sketch filter
	    const mediump vec3 W = vec3(0.2125, 0.7154, 0.0721);
	    mediump vec3 textureColor = rgb.xyz;

	    mediump vec2 stp0 = vec2(1.0 / resolution.x, 0.0);
	    mediump vec2 st0p = vec2(0.0, 1.0 / resolution.y);
	    mediump vec2 stpp = vec2(1.0 / resolution.x, 1.0 / resolution.y);
	    mediump vec2 stpm = vec2(1.0 / resolution.x, -1.0 / resolution.y);

	    mediump float i00   = dot( textureColor, W);
	    mediump float im1m1 = dot( texture2D(texture0, tx - stpp).rgb, W);
	    mediump float ip1p1 = dot( texture2D(texture0, tx + stpp).rgb, W);
	    mediump float im1p1 = dot( texture2D(texture0, tx - stpm).rgb, W);
	    mediump float ip1m1 = dot( texture2D(texture0, tx + stpm).rgb, W);
	    mediump float im10 = dot( texture2D(texture0, tx - stp0).rgb, W);
	    mediump float ip10 = dot( texture2D(texture0, tx + stp0).rgb, W);
	    mediump float i0m1 = dot( texture2D(texture0, tx - st0p).rgb, W);
	    mediump float i0p1 = dot( texture2D(texture0, tx + st0p).rgb, W);
	    mediump float h =-im1p1 - 2.0 * i0p1 - ip1p1 + im1m1 + 2.0 * i0m1 + ip1m1;
	    mediump float v =-im1m1 - 2.0 * im10 - im1p1 + ip1m1 + 2.0 * ip10 + ip1p1;

	    mediump float mag = 1.0 - length(vec2(h, v));
	    mediump vec3 target = vec3(mag);

      if (index == 6)
      	    gl_FragColor = vec4(vec3(1.0)-mix(textureColor, target, 0.8).xyz, 1.0);
      else if (index == 7)
      	    gl_FragColor = vec4(textureColor*.7 + vec3(1.0)-mix(textureColor, target, 0.8).xyz, 1.0);
      else if (index == 8)
      	    gl_FragColor = vec4(mix(textureColor, target, 0.5).xyz, 1.0);
      else if (index == 9)
      	    gl_FragColor = vec4(mix(textureColor, target, 0.8).xyz, 1.0);
	    return;
    }


	  gl_FragColor = vec4(rgb.xyz, 1.0);
    return;
    }
</script>

<script id="shader-8-fs" type="f">
    precision mediump float;
    varying vec2 vTextureCoord;
    uniform sampler2D texture0;
    uniform vec2 resolution;
    uniform vec2 mouse;
    uniform int index;
    uniform float time;
    uniform float Param1;
    uniform float Param2;
    uniform float Param3;

vec4 mod289(vec4 x){return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 perm(vec4 x){return mod289(((x * 34.0) + 1.0) * x);}

float noise3d(vec3 p)
{
    vec3 a = floor(p);
    vec3 d = p - a;
    d *= d * (3.0 - 2.0 * d);
    vec4 b = a.xxyy + vec4(0.0, 1.0, 0.0, 1.0);
    vec4 k1 = perm(b.xyxy);
    vec4 k2 = perm(k1.xyxy + b.zzww);
    vec4 c = k2 + a.zzzz;
    vec4 k3 = perm(c);
    vec4 k4 = perm(c + 1.0);
    vec4 o1 = fract(k3 * (1.0 / 41.0));
    vec4 o2 = fract(k4 * (1.0 / 41.0));
    vec4 o3 = o2 * d.z + o1 * (1.0 - d.z);
    vec2 o4 = o3.yw * d.x + o3.xz * (1.0 - d.x);
    return o4.y * d.y + o4.x * (1.0 - d.y);
}

    void main(void) {
    vec2 tx = 0.85 * (1.0-gl_FragCoord.xy / resolution);
    if (index == 0) // sin wave x
		  tx.x += sin(tx.y*15.0+time)/15.0;
	  else if (index == 1) // sin wave y
    	tx.y += sin(tx.x*10.0+time)/20.0;
    else if (index == 2)
    { // flag wave
  		mediump float x =sin((tx.y + time) * 8.0) * 0.003 + 0.0025*sin((tx.y + time) * 40.0);
  		mediump float y = 0.01*sin((tx.y + time) * 5.0) + 0.004*sin((tx.x + time) * 30.0);
  		tx = 0.79*(tx + vec2(y+0.11, x+0.11));
    }
    else if (index == 3) // mad warp
		  tx += vec2(noise3d(vec3(tx * 10.0, 0.0)), noise3d(vec3(tx * 10.0, 1.0))) * 0.05;
    else if (index == 4) // mad warp
		  tx += vec2(noise3d(vec3(tx * 7.0, 0.0)), noise3d(vec3(tx * 6.0, 1.0))) * 0.09;
    else if (index == 5) // mad warp
		  tx += vec2(noise3d(vec3(tx * 6.0, 0.0)), noise3d(vec3(tx * 5.0, 1.0))) * 0.09;
    if (index >= 3)
    {
      mediump float x =sin((tx.y + time) * 8.0) * 0.001 + 0.0015*sin((tx.y + time) * 40.0);
  		mediump float y = 0.001*sin((tx.y + time) * 5.0) + 0.0004*sin((tx.x + time) * 30.0);
  		tx = 0.79*(tx + vec2(y+0.11, x+0.11));
    }
  	gl_FragColor = texture2D(texture0, tx);
    return;
    }
</script>

<script id="shader-vs" type="v">
    attribute vec3 aVertexPosition;
    attribute vec2 aTextureCoord;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;

    varying vec2 vTextureCoord;

    void main(void) {
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vTextureCoord = aTextureCoord;
    }
</script>
</head>

<div id="container">
  <div id="main">
    <canvas id="webgl-canvas"></canvas>
    <div id="no-camera">No Webcam!</div>
    <div id="close-button">x</div>
    <splash class = "btn" enabled></splash>
<!--    <button enabled></button> -->
    <buttonRight disabled></buttonRight>
    <buttonLeft disabled></buttonLeft>
    <buttonUp disabled></buttonUp>
    <buttonDown disabled></buttonDown>
    </div>
  </div>
</body>

</html>
